# vim: filetype=zsh :

# LF CD ON EXIT
lfcd () {
    tmp="$(mktemp)"
    lf -last-dir-path="$tmp" "$@"
    if [ -f "$tmp" ]; then
        dir="$(cat "$tmp")"
        rm -f "$tmp"
        if [ -d "$dir" ]; then
            if [ "$dir" != "$(pwd)" ]; then
                cd "$dir"
            fi
        fi
    fi
}

bindkey -s '^o' 'lfcd\n'

c(){
	selfile=$(find -type d | fzf)
	cd "$selfile"
}

o(){
	selfile=$(fzf --preview 'bat {}')
	lf_opener "$selfile"
}

m(){
	man $(apropos --long "$1" | fzf | awk '{print $2, $1}' | tr -d '()')
}

cf(){
	selfile=$(fzf < ~/.config/configfiles | awk '{print $2}')
	nvim "$selfile"
}

v(){
	xtitle "st - (nvim) $PWD/$1"
	nvim $1 && xtitle "st"
}

sv(){
	xtitle "[SUDO] st - (nvim) $PWD/$1"
	sudo nvim $1 && xtitle "st"
}

s(){
	xtitle "st - [SUDO]"
	sudo $@ && xtitle "st"
}

f() {
    fff "$@"
    cd "$(cat "${XDG_CACHE_HOME:=${HOME}/.cache}/fff/.fff_d")"
}

newdoc(){
	projectname="$@"
	cp -r $HOME/.local/share/templates/latex-doc $HOME/dox/"$projectname"
	cd $HOME/dox/"$projectname"
	nvim Main.tex
}


# LF TMUX SELECT
_zlf() {
    emulate -L zsh
    local d=$(mktemp -d) || return 1
    {
        mkfifo -m 600 $d/fifo || return 1
        tmux split -bf zsh -c "exec {ZLE_FIFO}>$d/fifo; export ZLE_FIFO; exec lf" || return 1
        local fd
        exec {fd}<$d/fifo
        zle -Fw $fd _zlf_handler
    } always {
        rm -rf $d
    }
}
zle -N _zlf
# bindkey '\ek' _zlf
bindkey '^k' _zlf

_zlf_handler() {
    emulate -L zsh
    local line
    if ! read -r line <&$1; then
        zle -F $1
        exec {1}<&-
        return 1
    fi
    eval $line
    zle -R
}
zle -N _zlf_handler

# FZF-FUNCTIONS
fif(){
	grep --line-buffered -r "" $1 | fzf
}

fkill(){
    local pid
    if [ "$UID" != "0" ]; then
        pid=$(ps -f -u $UID | sed 1d | fzf -m | awk '{print $2}')
    else
        pid=$(ps -ef | sed 1d | fzf -m | awk '{print $2}')
    fi

    if [ "x$pid" != "x" ]
    then
        echo $pid | xargs kill -${1:-9}
    fi
}

fhist(){
	print -z $( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) | fzf +s --tac | sed -E 's/ *[0-9]*\*? *//' | sed -E 's/\\/\\\\/g')
}

bindkey -s '^h' 'fhist\n'

cheat(){
	curl cheat.sh/$1
}
